#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <OneWire.h>
#include <DallasTemperature.h>
#include <WiFiManager.h>
#include <HTTPClient.h>

#define SCREEN_WIDTH 128  // OLED width, in pixels
#define SCREEN_HEIGHT 64  // OLED height, in pixels
#define ONE_WIRE_BUS 23   // Pino de dados do sensor DS18B20
#define RELAY_PIN 27      // Pino de controle do relé

const char* apiUrl = "http://192.168.1.5:3000/dados";  // URL do servidor

Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);
OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature sensors(&oneWire);
HTTPClient http;  // Declara o objeto HTTPClient globalmente para reutilização

void setup() {
  Serial.begin(115200);

  WiFiManager wm;
  if (!wm.autoConnect("ESP32-Kazin", "admKazin123")) {
    Serial.println("Falha na conexão WiFi");
  } else {
    Serial.println("Conectado ao WiFi com sucesso!");
  }

  if (!oled.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    Serial.println(F("Falha ao iniciar o SSD1306 OLED"));
    while (1);
  }

  // Inicializa o sensor de temperatura e o relé
  sensors.begin();
  pinMode(RELAY_PIN, OUTPUT);
  digitalWrite(RELAY_PIN, LOW);

  // Configuração do HTTPClient para a requisição inicial
  http.begin(apiUrl);
  http.addHeader("Content-Type", "application/json");

  oled.clearDisplay();
  oled.display();
  oled.setTextSize(2);
  oled.setTextColor(SSD1306_WHITE);
}

void loop() {
  sensors.requestTemperatures();
  float temperature = sensors.getTempCByIndex(0);

  // Atualiza o display OLED
  oled.clearDisplay();
  oled.setCursor((SCREEN_WIDTH - 48) / 2 - 20, SCREEN_HEIGHT / 4);
  oled.print(temperature, 1);
  oled.print((char)247);
  oled.print("C");
  oled.display();

  // Envia a temperatura para a API
  String jsonData = "{\"temperatura\":" + String(temperature, 1) + "}" ;
  int httpResponseCode = http.POST(jsonData);

  if (httpResponseCode > 0) {
    Serial.print("Código de resposta HTTP: ");
    Serial.println(httpResponseCode);
    String payload = http.getString();
    Serial.println("Resposta da API: ");
    Serial.println(payload);
  } else {
    Serial.print("Erro ao enviar dados: ");
    Serial.println(httpResponseCode);
  }

  // Controle do relé com base na temperatura
  if (temperature < 20.0) {
    digitalWrite(RELAY_PIN, HIGH);
    Serial.println("Aquecedor LIGADO");
  } else if (temperature > 25.0) {
    digitalWrite(RELAY_PIN, LOW);
    Serial.println("Aquecedor DESLIGADO");
  }

  delay(10000);  // Aguarda 10 segundos antes da próxima leitura
}
